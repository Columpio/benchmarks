; Property from "Case-Analysis for Rippling and Inductive Proof",
; Moa Johansson, Lucas Dixon and Alan Bundy, ITP 2010
(declare-datatypes (a)
  ((list (nil) (cons (head a) (tail (list a))))))
(define-funs-rec
  ((par (a) (takeWhile ((x (=> a Bool)) (y (list a))) (list a))))
  ((match y
     (case nil (as nil (list a)))
     (case (cons z xs)
       (ite (@ x z) (cons z (takeWhile x xs)) (as nil (list a)))))))
(define-funs-rec
  ((par (a) (dropWhile ((x (=> a Bool)) (y (list a))) (list a))))
  ((match y
     (case nil (as nil (list a)))
     (case (cons z xs) (ite (@ x z) (dropWhile x xs) y)))))
(define-funs-rec
  ((par (a) (append ((x (list a)) (y (list a))) (list a))))
  ((match x
     (case nil y)
     (case (cons z xs) (cons z (append xs y))))))
(assert-not
  (par (a)
    (forall ((p (=> a Bool)) (xs (list a)))
      (= (append (takeWhile p xs) (dropWhile p xs)) xs))))
(check-sat)
